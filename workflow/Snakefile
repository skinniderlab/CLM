configfile: "config/config.yaml"
threads: 1
# -----------------------------------------------------------------------------
# Setup
# -----------------------------------------------------------------------------
PATHS=config['paths']
DATASET = os.path.splitext(os.path.basename(PATHS["dataset"]))[0]
REPRESENTATIONS = config["representations"]
FOLDS = config["folds"]
ENUM_FACTORS = config["enum_factors"]
OUTPUT_DIR = PATHS['output_dir']

shell.executable("/bin/bash")

wildcard_constraints:
    dataset=DATASET,
    repr='|'.join(REPRESENTATIONS),
    fold='\d+'

# -----------------------------------------------------------------------------

# -----------------------------------------------------------------------------
# Rules
# -----------------------------------------------------------------------------
rule plots:
    input:
        topk=expand("{output_dir}/model_evaluation/plot/{enum_factor}/topk",
            output_dir=OUTPUT_DIR, enum_factor=ENUM_FACTORS),
        calculate_outcomes_plot=expand("{output_dir}/model_evaluation/plot/{enum_factor}/calculate_outcomes",
            output_dir=OUTPUT_DIR, enum_factor=ENUM_FACTORS),
        nn_tc_plot=expand("{output_dir}/model_evaluation/plot/{enum_factor}/write_nn_tc",
            output_dir=OUTPUT_DIR, enum_factor=ENUM_FACTORS),
        train_discriminator_plot=expand("{output_dir}/model_evaluation/plot/{enum_factor}/train_discriminator",
            output_dir=OUTPUT_DIR, enum_factor=ENUM_FACTORS),
        freq_distribution_plot=expand("{output_dir}/model_evaluation/plot/{enum_factor}/freq_distribution",
            output_dir=OUTPUT_DIR, enum_factor=ENUM_FACTORS),
        calculate_outcome_distr_plot=expand("{output_dir}/model_evaluation/plot/{enum_factor}/calculate_outcome_distrs",
            output_dir=OUTPUT_DIR, enum_factor=ENUM_FACTORS),
        nn_tc_ever_v_never_plot=expand("{output_dir}/model_evaluation/plot/{enum_factor}/nn_tc_ever_v_never",
            output_dir=OUTPUT_DIR, enum_factor=ENUM_FACTORS),
        topk_tc_plot=expand("{output_dir}/model_evaluation/plot/{enum_factor}/topk_tc",
            output_dir=OUTPUT_DIR,enum_factor=ENUM_FACTORS)


include: "Snakefile_data"

rule prep_outcome_freq:
    """
    For sampled smiles in each fold, add a `bin` column denoting what
    frequency bin the sampled smile falls into.

    `max_molecules` is the max number of sampled smiles to consider for each
    frequency bin.
    """
    input:
        sample_file=PATHS['collect_tabulated_output'],
        known_smiles=PATHS['collect_known_smiles'],
        invalid_smiles=PATHS['collect_invalid_smiles'],
    output:
        "{output_dir}/model_evaluation/{enum_factor}/{dataset}_{repr}_{fold}_prep_outcome_freq.csv.gz",
    resources:
        mem_mb=120000,
        runtime=1000,
    shell:
        'clm prep_outcomes_freq '
        '--sample_file {input.sample_file} '
        '--output_file {output} '
        '--max_molecules 500000 '
        '--known_smiles {input.known_smiles} '
        '--invalid_smiles {input.invalid_smiles} '


rule calculate_outcomes:
    """
    For sampled smiles in each fold and for each bin within that fold,
    calculate certain distribution metrics based on a comparison between
    distribution of sampled smiles and distribution of training smiles in the
    fold.

    `max_orig_mols` is the max number of sampled smiles to consider for each
    frequency bin.
    """
    input:
        train_file=PATHS['train0_file'],
        sampled_file="{output_dir}/model_evaluation/{enum_factor}/{dataset}_{repr}_{fold}_prep_outcome_freq.csv.gz",
    output:
        output_file="{output_dir}/model_evaluation/{enum_factor}/{dataset}_{repr}_{fold}_calculate_outcomes.csv.gz",
    resources:
        mem_mb=256000,
        runtime=1000,
    shell:
        'clm calculate_outcomes '
        '--train_file {input.train_file} '
        '--sampled_file {input.sampled_file} '
        '--output_file {output.output_file} '
        '--seed 12 '


rule plot_calculate_outcomes:
    input:
        expand("{output_dir}/model_evaluation/{enum_factor}/{dataset}_{repr}_{fold}_calculate_outcomes.csv.gz",
            dataset=DATASET, repr=REPRESENTATIONS, fold=range(FOLDS), allow_missing=True)
    output:
        directory("{output_dir}/model_evaluation/plot/{enum_factor}/calculate_outcomes"),
    resources:
        mem_mb=256000,
        runtime=1000,
    shell:
        'clm plot calculate_outcomes '
        '--outcome_files {input} '
        '--output_dir {output} '


rule prep_nn_tc:
    input:
        sampled_file=PATHS['collect_tabulated_output'],
        pubchem_file=PATHS['pubchem_tsv_file'],
    output:
        output_file="{output_dir}/model_evaluation/{enum_factor}/{dataset}_{repr}_{fold}_prep_nn_tc.csv.gz",
    resources:
        mem_mb=64000,
        runtime=1000,
    shell:
        'clm prep_nn_tc_PubChem '
        '--sample_file {input.sampled_file} '
        '--pubchem_file {input.pubchem_file} '
        '--max_molecules 50000 '
        '--output_file {output.output_file} '


rule write_nn_tc:
    input:
        query_file="{output_dir}/model_evaluation/{enum_factor}/{dataset}_{repr}_{fold}_prep_nn_tc.csv.gz",
        reference_file=PATHS['train0_file'],
    output:
        output_file="{output_dir}/model_evaluation/{enum_factor}/{dataset}_{repr}_{fold}_write_nn_tc.csv.gz",
    resources:
        mem_mb=64000,
        runtime=1000,
    shell:
        'clm write_nn_Tc '
        '--query_file {input.query_file} '
        '--reference_file {input.reference_file} '
        '--output_file {output.output_file} '


rule plot_write_nn_tc:
    input:
        expand("{output_dir}/model_evaluation/{enum_factor}/{dataset}_{repr}_{fold}_write_nn_tc.csv.gz",
            dataset=DATASET, repr=REPRESENTATIONS, fold=range(FOLDS), allow_missing=True)
    output:
        directory("{output_dir}/model_evaluation/plot/{enum_factor}/write_nn_tc"),
    resources:
        mem_mb=256000,
        runtime=1000,
    shell:
        'clm plot write_nn_tc '
        '--outcome_files {input} '
        '--output_dir {output} '



rule train_discriminator:
    input:
        train_file=PATHS['train0_file'],
        sampled_file=PATHS['collect_tabulated_output'],
    output:
        output_file="{output_dir}/model_evaluation/{enum_factor}/{dataset}_{repr}_{fold}_train_discriminator.csv.gz",
    resources:
        mem_mb=64000,
        runtime=1000,
    shell:
        'clm train_discriminator '
        '--train_file {input.train_file} '
        '--sampled_file {input.sampled_file} '
        '--output_file {output.output_file} '


rule plot_train_discriminator:
    input:
        expand("{output_dir}/model_evaluation/{enum_factor}/{dataset}_{repr}_{fold}_train_discriminator.csv.gz",
            dataset=DATASET, repr=REPRESENTATIONS, fold=range(FOLDS), allow_missing=True)
    output:
        directory("{output_dir}/model_evaluation/plot/{enum_factor}/train_discriminator"),
    resources:
        mem_mb=256000,
        runtime=1000,
    shell:
        'clm plot train_discriminator '
        '--outcome_files {input} '
        '--output_dir {output} '


rule freq_distribution:
    input:
        sampled_file=PATHS['collect_tabulated_output'],
        test_file=PATHS['test0_file'],
    output:
        output_file="{output_dir}/model_evaluation/{enum_factor}/{dataset}_{repr}_{fold}_freq_distribution.csv.gz",
    resources:
        mem_mb=64000,
        runtime=1000,
    shell:
        'clm write_freq_distribution '
        '--sampled_file {input.sampled_file} '
        '--test_file {input.test_file} '
        '--output_file {output.output_file} '


rule plot_freq_distribution:
    input:
        expand("{output_dir}/model_evaluation/{enum_factor}/{dataset}_{repr}_{fold}_freq_distribution.csv.gz",
            dataset=DATASET, repr=REPRESENTATIONS, fold=range(FOLDS), allow_missing=True)
    output:
        directory("{output_dir}/model_evaluation/plot/{enum_factor}/freq_distribution"),
    resources:
        mem_mb=256000,
        runtime=1000,
    shell:
        'clm plot freq_distribution '
        '--outcome_files {input} '
        '--output_dir {output} '


rule write_outcome_distr:
    input:
        sample_file=PATHS['collect_tabulated_output'],
        train_file=PATHS['train0_file'],
        pubchem_file=PATHS['pubchem_tsv_file'],
    output:
        output_file="{output_dir}/model_evaluation/{enum_factor}/{dataset}_{repr}_{fold}_write_outcome_distr.csv.gz",
    resources:
        mem_mb=64000,
        runtime=1000,
    shell:
        'clm write_outcome_distr '
        '--sample_file {input.sample_file} '
        '--train_file {input.train_file} '
        '--pubchem_file {input.pubchem_file} '
        '--max_mols 100000 '
        '--output_file {output.output_file} '


rule calculate_outcome_distrs:
    input:
        input_file="{output_dir}/model_evaluation/{enum_factor}/{dataset}_{repr}_{fold}_write_outcome_distr.csv.gz",
    output:
        output_file="{output_dir}/model_evaluation/{enum_factor}/{dataset}_{repr}_{fold}_calculate_outcome_distrs.csv.gz",
    resources:
        mem_mb=64000,
        runtime=1000,
    shell:
        'clm calculate_outcome_distrs '
        '--input_file {input.input_file} '
        '--output_file {output.output_file} '


rule plot_outcome_distributions:
    input:
        expand("{output_dir}/model_evaluation/{enum_factor}/{dataset}_{repr}_{fold}_calculate_outcome_distrs.csv.gz",
            dataset=DATASET, repr=REPRESENTATIONS, fold=range(FOLDS), allow_missing=True)
    output:
        directory("{output_dir}/model_evaluation/plot/{enum_factor}/calculate_outcome_distrs"),
    resources:
        mem_mb=256000,
        runtime=1000,
    shell:
        'clm plot calculate_outcome_distrs '
        '--outcome_files {input} '
        '--output_dir {output} '

rule nn_tc_ever_v_never:
    input:
        query_file=PATHS['train0_file'],
        reference_file=PATHS['train0_file'],
    output:
        output_file="{output_dir}/model_evaluation/{enum_factor}/{dataset}_{repr}_{fold}_nn_tc_ever_v_never.csv.gz",
    resources:
        mem_mb=64000,
        runtime=1000,
    shell:
        'clm write_nn_Tc '
        '--query_file {input.query_file} '
        '--reference_file {input.reference_file} '
        '--output_file {output.output_file} '


rule plot_nn_tc_ever_v_never:
    input:
        nn_tc_file = expand("{output_dir}/model_evaluation/{enum_factor}/{dataset}_{repr}_{fold}_nn_tc_ever_v_never.csv.gz",
            dataset=DATASET, repr=REPRESENTATIONS, fold=range(FOLDS), allow_missing=True),
        ranks_file = expand(PATHS['overall_ranks_file'],
            min_freq = 1, metric='freq-avg', dataset=DATASET, repr=REPRESENTATIONS, allow_missing=True)
    output:
        directory("{output_dir}/model_evaluation/plot/{enum_factor}/nn_tc_ever_v_never"),
    resources:
        mem_mb=256000,
        runtime=1000,
    shell:
        'clm plot nn_tc_ever_v_never '
        '--outcome_files {input.nn_tc_file} '
        '--ranks_file {input.ranks_file} '
        '--output_dir {output} '


rule plot_topk_tc:
    input:
        expand(PATHS['overall_tc_file'],
            min_freq = 1, metric='freq-avg', dataset=DATASET, repr=REPRESENTATIONS, allow_missing=True)
    output:
        directory("{output_dir}/model_evaluation/plot/{enum_factor}/topk_tc"),
    resources:
        mem_mb=256000,
        runtime=1000,
    shell:
        'clm plot topk_tc '
        '--outcome_files {input} '
        '--output_dir {output} '


rule forecast:
    input:
        test_file=PATHS['test0_file'],
        sample_file=PATHS['collect_tabulated_output']
    output:
        output_file="{output_dir}/model_evaluation/{enum_factor}/{dataset}_{repr}_{fold}_forecast.csv",
    resources:
        mem_mb=64000,
        runtime=30,
    shell:
        'clm forecast '
        '--test_file {input.test_file} '
        '--sample_file {input.sample_file} '
        '--output_file {output.output_file} '


rule plot_topk:
    input:
        expand(PATHS['overall_ranks_file'],
            min_freq = 1, metric='freq-avg', dataset=DATASET, repr=REPRESENTATIONS, allow_missing=True)
    output:
        directory("{output_dir}/model_evaluation/plot/{enum_factor}/topk"),
    resources:
        mem_mb=256000,
        runtime=1000,
    shell:
        'clm plot topk '
        '--outcome_files {input} '
        '--output_dir {output} '
